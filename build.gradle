buildscript {
    repositories {
        jcenter()
        maven {
      url "https://plugins.gradle.org/m2/"
    }
        
    }
    dependencies {
        classpath "com.github.townsfolk:gradle-release:1.2"
        classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:1.2"
    }
}

apply plugin: "java"
apply plugin: "groovy"
apply plugin: "maven"
apply plugin: "maven-publish"
apply plugin: "com.jfrog.bintray"

repositories {
    jcenter()
}

dependencies {
    compile "io.dropwizard:dropwizard-core:0.8.1"
    compile "io.dropwizard:dropwizard-db:0.8.1"
    compile "com.commercehub.dropwizard:dropwizard-mongo:2.0.0"
    compile "org.mongeez:mongeez:0.9.4"
    testCompile "org.spockframework:spock-core:0.7-groovy-2.0"
}

task sourcesJar(type: Jar, dependsOn: classes) {
    from sourceSets.main.allSource
    classifier "sources"
    extension "jar"
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    from javadoc.destinationDir
    classifier "javadoc"
    extension "jar"
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            artifact sourcesJar {
                classifier "sources"
            }

            artifact javadocJar {
                classifier "javadoc"
            }

            pom.withXml {
                asNode().dependencies.'*'.findAll() {
                    it.scope.text() == 'runtime' && project.configurations.compile.allDependencies.find { dep ->
                        dep.name == it.artifactId.text()
                    }
                }.each() {
                    it.scope*.value = 'compile'
                }
            }
        }
    }
}

group = "com.commercehub.dropwizard"
sourceCompatibility = "1.7"

apply plugin: "release"
apply plugin: "com.jfrog.bintray"

bintray {
    user = project.hasProperty("bintrayUserName") ? bintrayUserName : null
    key = project.hasProperty("bintrayApiKey") ? bintrayApiKey : null
    publications = ["mavenJava"]
    pkg {
        repo = "main"
        userOrg = "commercehub-oss"
        name = "dropwizard-mongeez"
        desc = "A Dropwizard bundle for using mongeez to manage MongoDB data migrations."
        licenses = ['Apache-2.0']
        labels = ["dropwizard", "mongeez", "mongodb", "mongo"]
    }
}

bintrayUpload.dependsOn build, sourcesJar, javadocJar
createReleaseTag.dependsOn bintrayUpload
